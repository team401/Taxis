plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    id "com.palantir.git-version" version "0.10.0"
    id "com.jfrog.bintray" version "1.8.4"
}

group = 'org.team401'
version = gitVersion()

sourceCompatibility = 1.8

ext.snakeskinVersion = "3.0beta5"
ext.wpiVersion = "2020.1.2"

repositories {
    mavenCentral()

    maven {
        url "http://dl.bintray.com/team401/SnakeSkin"
    }

    maven {
        url "https://frcmaven.wpi.edu/artifactory/release/"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.snakeskin:SnakeSkin-Core:$snakeskinVersion"
    implementation "org.snakeskin:SnakeSkin-FRC:$snakeskinVersion"

    implementation "edu.wpi.first.wpilibj:wpilibj-java:$wpiVersion"
    implementation "edu.wpi.first.hal:hal-java:$wpiVersion"
    implementation "edu.wpi.first.ntcore:ntcore-java:$wpiVersion"

    api "gov.nist.math:jama:1.0.3"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//Jars built for github releases
jar {
    destinationDir = file("$rootDir/out/libs")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        Taxis(MavenPublication) {
            from components.java
            groupId 'org.team401'
            artifactId 'Taxis'
            artifact sourcesJar
            version this.version
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv("BINTRAY_USER") != null ? System.getenv("BINTRAY_USER") : ""
    key = project.hasProperty('key') ? project.property('key'): System.getenv("BINTRAY_KEY") != null ? System.getenv("BINTRAY_KEY") : ""
    publications = ['Taxis']
    pkg {
        repo = "SnakeSkin"
        name = "Taxis"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/taxis"
        version {
            name = this.version
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

wrapper {
    gradleVersion = '6.0.1'
}