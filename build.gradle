plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.20'
    id "com.palantir.git-version" version "0.10.0"
    id "com.jfrog.bintray" version "1.8.4"
}

group = 'org.team401'
version = gitVersion()

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url "http://dl.bintray.com/team401/SnakeSkin"
    }

    maven {
        url "http://first.wpi.edu/FRC/roborio/maven/release"
    }

    maven {
        url "http://devsite.ctr-electronics.com/maven/release/"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.snakeskin:SnakeSkin-Core:2.0rc4"
    implementation "org.snakeskin:SnakeSkin-FRC:2.0rc4"
    implementation "org.snakeskin:SnakeSkin-CTRE:2.0rc4"
    
    implementation "edu.wpi.first.hal:hal-java:2019.2.1"
    implementation "edu.wpi.first.ntcore:ntcore-java:2019.2.1"
    implementation "com.ctre.phoenix:api-java:5.13.0"

    api "gov.nist.math:jama:1.0.3"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//Jars built for github releases
jar {
    destinationDir = file("$rootDir/out/libs")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        Taxis(MavenPublication) {
            from components.java
            groupId 'org.team401'
            artifactId 'Taxis'
            artifact sourcesJar
            version this.version
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv("BINTRAY_USER") != null ? System.getenv("BINTRAY_USER") : ""
    key = project.hasProperty('key') ? project.property('key'): System.getenv("BINTRAY_KEY") != null ? System.getenv("BINTRAY_KEY") : ""
    publications = ['Taxis']
    pkg {
        repo = "SnakeSkin"
        name = "Taxis"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/taxis"
        version {
            name = this.version
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

wrapper {
    gradleVersion = '4.10.3'
}